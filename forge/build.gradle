plugins {
	id 'multiloader-loader'
	id 'net.neoforged.moddev.legacyforge'
	id 'com.modrinth.minotaur' version '2.+'
}

mixin {
	add(sourceSets.main, "${mod_id}.refmap.json")

	config("${mod_id}.mixins.json")
}

legacyForge {
	version = "${minecraft_version}-${forge_version}"

	validateAccessTransformers = true

	def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
	if (at.exists()) {
		accessTransformers = ["src/main/resources/META-INF/accesstransformer.cfg"]
	}
	parchment {
		minecraftVersion = parchment_minecraft
		mappingsVersion = parchment_version
	}
	runs {
		client {
			client()
		}
		data {
			data()
			programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
		}
		server {
			server()
		}
	}

	mods {
		"${mod_id}" {
			sourceSet sourceSets.main
		}
	}
}

sourceSets.main.resources.srcDir 'src/generated/resources'

dependencies {
	compileOnly project(":common")
	annotationProcessor("org.spongepowered:mixin:0.8.5-SNAPSHOT:processor")

	implementation "thedarkcolour:kotlinforforge:4.10.0"

	implementation fg.deobf("foundry.veil:Veil-forge-${minecraft_version}:${veil_version}") {
		transitive = false
	}
	jarJar ("foundry.veil:Veil-forge-${minecraft_version}:${veil_version}") {
		jarJar.ranged(it, "[${veil_version},)")
	}

	modCompileOnly "org.valkyrienskies.core:api:${vs_core_version}"
	modCompileOnly "org.valkyrienskies:valkyrienskies-120-forge:${vs2_version}"
	modRuntimeOnly "org.valkyrienskies.core:api:${vs_core_version}"
	modRuntimeOnly "org.valkyrienskies:valkyrienskies-120-forge:${vs2_version}"

	modCompileOnly "maven.modrinth:jade:${jade_version}+forge"
	modRuntimeOnly "maven.modrinth:jade:${jade_version}+forge"
}

jar {
	finalizedBy('reobfJar')
	manifest.attributes([
		"MixinConfigs": "${mod_id}.mixins.json"
	])
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "cosmicpluse"
	versionNumber = "${minecraft_version}-forge-${version}"
	versionType = "release"
	uploadFile = jar
	gameVersions = ["1.20.1"]
	loaders = ["forge", "neoforge"]
	dependencies {
		// required.project "jade"
		// required.project "valkyrien-skies"
	}
}
